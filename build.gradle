plugins {
  id 'com.github.johnrengelman.shadow' version '8.1.1'
  id 'java'
}

group = 'org.creativecraft'
version = '1.0.7'

java {
  sourceCompatibility = JavaVersion.VERSION_16
  targetCompatibility = JavaVersion.VERSION_16
}

repositories {
  mavenCentral()
  maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/public/' }
  maven { url = 'https://oss.sonatype.org/content/groups/public/' }
  maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
  maven { url = 'https://repo.minebench.de/' }
  maven { url = 'https://maven.enginehub.org/repo/' }
}

dependencies {
  compileOnly 'org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT'
  compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.0'
  implementation 'co.aikar:acf-bukkit:0.5.1-SNAPSHOT'
  implementation 'de.themoep:minedown:1.7.1-SNAPSHOT'
  implementation 'org.bstats:bstats-bukkit-lite:1.8'
}

shadowJar {
  relocate 'co.aikar.commands', 'org.creativecraft.celebrate.acf'
  relocate 'co.aikar.locales', 'org.creativecraft.celebrate.locales'
  relocate 'de.themoep.minedown', 'org.creativecraft.celebrate.minedown'
  relocate 'org.bstats', 'org.creativecraft.celebrate.bstats'
}

processResources {
  def props = [version: version]
  inputs.properties props
  filteringCharset 'UTF-8'
  filesMatching('plugin.yml') {
    expand props
  }
}

build.dependsOn shadowJar
